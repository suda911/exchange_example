apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.objectbox'

android.sourceSets.main.java.srcDirs += 'build/generated/source/objectbox'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.0"
    defaultConfig {
        applicationId "com.gligent.exchange"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    android.buildTypes.each { type ->
        type.buildConfigField 'String', 'BASE_URL', WEBServiceBaseURL
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    sourceSets {
        main.java.srcDirs += 'build/generated/source/objectbox'
    }
}

ext.versions = [
        coreKtx   : "1.1.0",
        coroutines: "1.3.0-M2",
        lifecycle : "2.0.0",
        retrofit  : "2.6.0",
        okHttp    : "4.0.1",
        dagger    : "2.24"
]


ext.coroutines = [
        core   : "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.coroutines",
        android: "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutines"
]

ext.arch = [
        extensions: "androidx.lifecycle:lifecycle-extensions:$versions.lifecycle",
        compiler  : "androidx.lifecycle:lifecycle-compiler:$versions.lifecycle",
        common    : "androidx.lifecycle:lifecycle-common-java8:$versions.lifecycle",
        kt        : "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"
]

ext.kotlin = [
        stdlib: "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version",
        core  : "androidx.core:core-ktx:$versions.coreKtx"
]

ext.retrofit = [
        retrofit   : "com.squareup.retrofit2:retrofit:$versions.retrofit",
        gson       : "com.squareup.retrofit2:converter-gson:$versions.retrofit",
        rxjava2    : "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofit",
        interceptor: "com.squareup.okhttp3:logging-interceptor:$versions.okHttp",
        okhttp     : "com.squareup.okhttp3:okhttp:$versions.okHttp"
]

ext.dagger = [
        dagger    : "com.google.dagger:dagger:$versions.dagger",
        compiler  : "com.google.dagger:dagger-compiler:$versions.dagger",
        annotation: 'javax.annotation:jsr250-api:1.0'
]

ext.design = [
        appcompat       : 'androidx.appcompat:appcompat:1.1.0',
        annotation      : 'androidx.annotation:annotation:1.1.0',
        material        : 'com.google.android.material:material:1.0.0',
        cardview        : 'androidx.cardview:cardview:1.0.0',
        recyclerview    : 'androidx.recyclerview:recyclerview:1.0.0',
        constraintlayout: 'androidx.constraintlayout:constraintlayout:1.1.3',
        viewpager2      : "androidx.viewpager2:viewpager2:1.0.0-rc01",
        number_edit_text: 'ru.softbalance.widgets:NumberEditText:1.1.2'
]

ext.db = [
        objectbox: "io.objectbox:objectbox-kotlin:$objectboxVersion"
]

ext.test = [
        junit         : 'junit:junit:4.12',
        runner        : 'androidx.test:runner:1.3.0-alpha02',
        espresso      : 'androidx.test.espresso:espresso-core:3.3.0-alpha02',
        core          : "android.arch.core:core-testing:1.1.1",
        mockito       : "org.mockito:mockito-core:2.13.0",
        truth         : "com.google.truth:truth:0.36",
        inline        : 'org.mockito:mockito-inline:2.8.47',
        coroutines    : 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.2',
        mockito_kotlin: "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
]

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //kt
    implementation kotlin.stdlib
    implementation kotlin.core

    implementation coroutines.core
    implementation coroutines.android

    //test
    testImplementation test.junit
    androidTestImplementation test.runner
    androidTestImplementation test.espresso
    testImplementation test.core
    testImplementation test.mockito
    testImplementation test.truth
    testImplementation test.inline
    testImplementation test.coroutines
    testImplementation test.mockito_kotlin

    //design
    implementation design.appcompat
    implementation design.annotation
    implementation design.material
    implementation design.cardview
    implementation design.recyclerview
    implementation design.constraintlayout
    implementation design.viewpager2
    implementation design.number_edit_text

    //retrofit
    implementation retrofit.retrofit
    implementation retrofit.gson
    implementation retrofit.rxjava2
    implementation retrofit.interceptor
    implementation retrofit.okhttp

    //dagger
    implementation dagger.dagger
    kapt dagger.compiler
    compileOnly dagger.annotation

    //arch
    implementation arch.extensions
    annotationProcessor arch.compiler
    implementation arch.common
    implementation arch.kt

    implementation db.objectbox

    implementation("io.reactivex.rxjava2:rxkotlin:2.4.0")

}

kapt {
    generateStubs = true
}
